<div align='center'><h1>Conjetura de Collatz</h1></div>

1. ```haskell
   satisfaceCollatz :: Integer -> Integer
   ```
   
   Dados dos naturales `n` y `m`, determina si `n` satisface la conjetura de Collatz en menos de `m` pasos.
   
   

2. ```haskell
   satisfaceCollatzHasta :: Integer -> Integer -> Bool
   ```
   
   Dados dos naturales, `n` y `k`, determina si todos los `i` entre 1 y `n`, satisfacen la conjetura de Collatz en menos de `m` pasos.

3. ```haskell
   cantidadTerminosPares :: Integer -> Integer
   ```
   
   Sea `n` un natural que satisface la conjetura de Collatz, determinar cuantos de los términos de su secuencia son pares, hasta que llegue a 1.
   
   

4. ```haskell
   largoSecuencia :: Integer -> Integer
   ```
   
   Sea `n` un natural que satisface la conjetura de Collatz, determinar la cantidad de pasos totales que deben realizarse para que llegue a 1.

5. ```haskell
   secuenciaMasLargaHasta :: Integer -> Integer
   ```
   
   Dado un natural `n`, determinar cuál es el mínimo `i` entre 1 y `n` que posee la secuencia más larga.
   
   

---

<div align='center'><h3>Desarrollo de los ejercicios</h3></div>

Primero, para trabajar con la [conjetura de Collatz](https://www.wikiwand.com/en/Collatz_conjecture) es necesario
crear una función que sea capaz de tomar un natural `n` y dar el próximo natural de la secuencia, siguiendo las susodichas reglas.

Para eso, definimos:

```haskell
-- Dado un n natural, genera el próximo número en la secuencia
proximoCollatz :: Integer -> Integer
proximoCollatz n | n <= 0 = undefined
                 | esPar n   = div n 2
                 | otherwise = (3 * n) + 1
```

Lo cual es directamente la definición de la secuencia en cuestión.

```
proximoCollatz 16 -> 8
proximoCollatz 8  -> 4
proximoCollatz 4  -> 2
proximoCollatz 2  -> 1
proximoCollatz 1  -> 4
```

1. La forma _naïve_ de corroborar que un número satisface la conjetura en `m` pasos es:

```
1 - Dado un natural n, corroborar la secuencia de Collatz hasta a_k = 1 en k pasos.
2 - Comparar, ¿Es m > k? Si es así, entonces n llega a 1 en m pasos o menos (al menos k). Sino, no lo hace.
```

   El problema de éste abordaje es que primero recorremos k pasos siempre que queremos saber si se corrobora la conjetura en `m` pasos o no.
   En vez de eso, podemos corroborarlo en `m` pasos:

```
1 - Dado un natural n, corroborar m pasos de la secuencia.
2 - Si a_m = 1, entonces n corrobora la conjetura en m pasos. De lo contrario, si a_m != 1, entonces no lo hace.
```

  De esta manera, sólo recorremos `m` pasos la secuencia.
  Podemos hacerlo de la siguiente manera:

```haskell
-- Dados n, m naturales determina si n satisface la conjetura de Collatz en menos de m pasos
satisfaceCollatz :: Integer -> Integer -> Bool
satisfaceCollatz 1 _ = True
satisfaceCollatz n 0 = n == 1
satisfaceCollatz n m = satisfaceCollatz (proximoCollatz n) (m-1)
```

  Primero, los casos base:
  - `n = 1` siempre dará `True`, ya que `1` satisface la secuencia de Collatz en **0 pasos**. Como `m > 0`, siempre será verdadero.
  - `m = 0` implica que debemos dar el valor de verdad de `n == 1`, que es lo mismo que decir _¿Corrobora `n` la conjetura en `0` pasos?_.

  Luego, queremos avanzar `1` paso en la secuencia de Collatz (Por lo que pedimos `proximoCollatz n`), y por ende, reducir la cantidad de pasos en la
  que debe corroborarse el nuevo `n`.

  Dada esta recurrencia, vamos _reduciendo_ el problema hasta llegar al caso base.
```
 Ejemplo con n = 13, p = 10
 Como n != 1 y p != 0, y sabiendo que (proximoCollatz 13) = 40
 -> satisfaceCollatz 40 9 
 -> satisfaceCollatz 20 8 
 -> satisfaceCollatz 10 7 
 -> satisfaceCollatz 5 6
 -> satisfaceCollatz 16 4
 -> satisfaceCollatz 8 3
 -> satisfaceCollatz 4 2
 -> satisfaceCollatz 2 1
 -> satisfaceCollatz 1 0 -> True

 Ejemplo con n = 12, p = 5
 Como n != 1 y p != 0, y sabiendo que (proximoCollatz 12) = 6
 -> satisfaceCollatz 6 4 
 -> satisfaceCollatz 3 3 
 -> satisfaceCollatz 10 2 
 -> satisfaceCollatz 5 1 
 -> satisfaceCollatz 4 0 -> 4 != 1 -> False 
```


2. Para corroborar que los números entre `1` y `n` satisfagan Collatz en `m` pasos, podemos hacer uso de funciones auxiliares.
En particular, podemos pensar a `satisfaceCollatzHasta` como un caso especial de una función que corrobore a todos los `i` entre `a` y `b` que satisfagan
la conjetura en menos de `m` pasos. Así, podemos formular la siguiente función:

```haskell
intervaloSatisfaceCollatz :: Integer -> Integer -> Integer -> Bool
intervaloSatisfaceCollatz a b m | (a > b) = undefined
                                | a == b = c
                                | otherwise = (intervaloSatisfaceCollatz (a+1) b m) && c
                                where c = satisfaceCollatz a m
```

  Primero, el caso base:
  - `a = b` implica que el `i` que pedimos está en el último valor que puede tomar, por ende sólo corroboramos que satisfaga la conjetura en `m` pasos.
  - Además, `a > b` no tiene sentido.

  Luego queremos recorrer todos los `i` desde `a` hasta `b`. Podemos pensar que `b` está _fijo_ y nos movemos desde `a`. Todos los `i` se comparan 
  contra la misma cantidad de pasos `m`. Lo que nos interesa es - de cierta manera - añadir los valores de verdad del `i` actual, y el próximo. Para ello,
  tomamos el _`y lógico`_ entre los valores de verdad del `i` actual y el próximo.

```
 Ejemplo con a = 5, b = 7, m = 15
 Como a < b y a != m, entonces:
 
 (intervaloSatisfaceCollatz 6 7 15) && (satisfaceCollatz 5 15)
 
 Por un lado, satisfaceCollatz 5 15 -> True
 
 Luego, intervaloSatisfaceCollatz 6 7 15 
 
 -> intervaloSatisfaceCollatz 7 7 15 && satisfaceCollatz 6 15
    -> Por un lado, satisfaceCollatz 6 15 -> True
    -> Luego, intervaloSatisfaceCollatz 7 7 15 -> satisfaceCollatz 7 15 -> False
    -> Entonces, True && False -> False
 -> Por ende, intervaloSatisfaceCollatz 5 7 15 -> False

 Ahora bien, probemos con a = 5, b = 7, m = 16.
 Luego, basta con ver que satisfaceCollatz 7 16 es verdadero.
 
 -> satisfaceCollatz 7 16 -> True
 -> Luego, True && True -> True
 
 Por ende intervaloSatisfaceCollatz 5 7 16 -> True
 ```

  Teniendo ésto, ahora `satisfaceCollatzHasta` es simplemente un caso especial de `intervaloSatisfaceCollatz`. Tal que:

```haskell
-- Dados n,k naturales, determina si todos los i (1 <= i <= n) satisfacen Collatz en menos de m pasos
satisfaceCollatzHasta :: Integer -> Integer -> Bool
satisfaceCollatzHasta n m = intervaloSatisfaceCollatz 1 n m
```

3. Para saber la cantidad de términos pares, primero necesitamos entender cómo calcular los pasos totales para que, dado un número, la secuencia según
el problema de la conjetura, converja a 1. Por ende, resolveremos primero el ítem 4. Luego, los pasos pares simplemente serán casos especiales del
problema general _"contar todos los pasos de la secuencia de Collatz de un número natural"_.

   Para eso, podemos pensar lo siguiente:
   Sea `n` un natural, tal que `c` es el resultado de haber dado `1` paso sobre `n` usando la definición de la conjetura.
   Para cada `c != 1`, el largo de la secuencia incrementa en `1`.
   
   Además, definimos que si `n = 1`, el largo de su secuencia es `0`. Ya que necesitamos `0` pasos desde el `1` para llegar al `1`. Es decir,
   no es necesario _hacer nada_.
   
   Así, podemos formular:
```haskell
   -- Dado n entero, calcula la cantidad de pasos hasta llegar a 1.
   largoSecuencia :: Integer -> Integer
   largoSecuencia n | n == 1 = 0
                    | otherwise = (largoSecuencia c) + 1
                    where c = proximoCollatz n
```
   
```
Ejemplo con n = 4:
 - n != 1 -> (largoSecuencia 2) + 1
 - n != 1 -> (largoSecuencia 1) + 1
 - n == 1 -> 0

Como (largoSecuencia 1) = 0
Entonces (largoSecuencia 2) = 1 + 1 = 2
Y por ende, (largoSecuencia 4) = 2 + 1 = 3 
```

  Ahora, para calcular cuales de esos pasos fueron _pares_ podemos definir una función `cantidadTerminosPares` de la siguiente manera:
  
```haskell
esPar :: Integer -> Bool
esPar n = mod n 2 == 0

cantidadTerminosPares :: Integer -> Integer
cantidadTerminosPares n | n == 1 = 0
                        | esPar n = (cantidadTerminosPares c) + 1
                        | otherwise = (cantidadTerminosPares c)
                        where c = proximoCollatz n
```

Donde sólo añadimos `1` al largo de la secuencia, si el término fue par.

5. Dada una secuencia comprendida entre `1` y `n`, podemos aplicar la reducción que utilizamos en `satisfaceCollatzHasta`. Es decir, podemos generalizar
éste problema a _"¿Cuál es el `i` comprendido entre `a` y `b` cuya secuencia sea la más larga de dicho intervalo?"_.

   Para eso, de manera similar, planteamos:

```haskell
maximo :: Integer -> Integer -> Integer
maximo a b | a >= b = a
           | otherwise = b

intervaloLargoSecuencias :: Integer -> Integer -> Integer
intervaloLargoSecuencias a b | (a > b) = undefined
                             | a == b = l
                             | otherwise = maximo (intervaloLargoSecuencias (a+1) b) (l)
                             where l = largoSecuencia a
```

  Donde buscamos todos los largos `l` de cada secuencia de `i`, el cuál recorre los números `a` hasta `b`. La forma de _"añadir"_ el paso
  recursivo aquí, es tomar el máximo entre el largo de la secuencia del `i` actual, y el próximo.
  
```
Ejemplo con a = 2, b = 4.
 - a != b -> maximo (intervaloLargoSecuencias 3 4) 1
 Luego, intervaloLargoSecuencias 3 4 -> maximo (intervaloLargoSecuencias 4 4) 7
 Luego, como en intervaloLargoSecuencias 4 4, a = b -> 2
 Luego, intervaloLargoSecuencias 3 4 -> maximo 2 7 -> 7
```
  
  Ahora, podemos dar el caso especial de `1` hasta `n` tal que:
  
```haskell
longitudMasLargaHasta :: Integer -> Integer
longitudMasLargaHasta n = intervaloLargoSecuencias 1 n
```

  Ahora bien, lo que obtenemos de `longitudMasLargaHasta n` es la longitud más larga de toda la secuencia. Lo que necesitamos es el número más pequeño que dió esa secuencia.
  Para ello, podemos recorrer la secuencia de vuelta, buscando el primer número con ese largo de secuencia.
  Nuevamente, generalizaremos el problema primero creando una función auxiliar que pueda buscar el `i` con largo `l` más chico desde un `n` determinado.
  
  Así, definimos:
  
```haskell
  minimoNumeroConSecuenciaDesde :: Integer -> Integer -> Integer
  minimoNumeroConSecuenciaDesde n m | l == m = n
                                    | otherwise = minimoNumeroConSecuenciaDesde (n+1) m
                                    where
                                      l = largoSecuencia n
```

  De esta manera, podemos buscar el primer `i` que tenga el largo igual al mayor largo de todas las secuencias entre `n` y `m`.
  
```haskell
secuenciaMasLargaHasta :: Integer -> Integer
secuenciaMasLargaHasta m = minimoNumeroConSecuenciaDesde 1 (longitudMasLargaHasta m)
```
